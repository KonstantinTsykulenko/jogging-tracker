ext {
    springBootVersion = '1.1.4.RELEASE'
    springSecurityVerson = '3.2.4.RELEASE'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.1.4.RELEASE"
        classpath 'com.eriwen:gradle-js-plugin:1.12.0'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'groovy'
apply plugin: 'js'

def jsSrcDir = "${sourceSets.main.resources.srcDirs.iterator().next()}/static/client/js"

allprojects {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8
}

javascript.source {
    dev {
        js {
            srcDir jsSrcDir
            include "*.js"
            exclude "*.min.js"
        }
    }
    prod {
        js {
            srcDir jsSrcDir
            include "*.min.js"
        }
    }
}

combineJs {
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/all.js")
}

minifyJs {
    source = combineJs
    dest = file("${buildDir}/js/static/client/js/app.min.js")
    sourceMap = file("${buildDir}/all.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
    }
}


if (!project.hasProperty('jsDebug')) {
    processResources.dependsOn(['minifyJs'])

    processResources.doLast {
        copy {
            from "${buildDir}/js"
            into "${buildDir}/resources/main"
        }
    }
}

processResources {
    if (project.hasProperty('jsDebug')) {
        rename 'dev.index.html', 'index.html'
        exclude 'static/client/index.html'
    }
    else {
        exclude "static/client/js"
        exclude 'static/client/dev.index.html'
    }
    if (project.hasProperty('dev')) {
        rename 'dev.application.properties', 'application.properties'
        exclude 'application.properties'
    } else {
        exclude 'dev.application.properties'
    }
}

applicationDefaultJvmArgs = [
        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=7777"
]

jar {
    baseName = 'jogging-tracker'
    version = '0.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile "org.springframework.boot:spring-boot-starter-jetty:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    //compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"

    compile "org.springframework.security:spring-security-web:$springSecurityVerson"
    compile "org.springframework.security:spring-security-config:$springSecurityVerson"

    compile "org.hibernate:hibernate-validator:5.0.3.Final"
    //compile "org.hsqldb:hsqldb:2.3.2"
    compile "postgresql:postgresql:9.1-901-1.jdbc4"

    compile "com.google.guava:guava:17.0"

    testCompile "org.springframework:spring-test:4.0.6.RELEASE"
    testCompile "org.codehaus.groovy:groovy-all:2.2.0"
    testCompile "com.fasterxml.jackson.core:jackson-databind:2.3.3"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7+"
    testCompile "cglib:cglib-nodep:3.1"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

build {
    mustRunAfter 'clean'
}

task stage(dependsOn: ['clean', 'build'])